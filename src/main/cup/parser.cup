package org.ifn660.jflexer;

import java_cup.runtime.*;
import org.ifn660.jflexer.ast.*;
import java.util.ArrayList;
import java.util.List;



/* Symbol lists
These declarations name and supply an object type for each terminal and non-terminal symbol that appears in the grammar
Terminals are returned by the scanner and placed on the parse stack
Non-terminals replace a series of Symbol objects on the parse stack when the RHS of a production is recognised
*/

parser code 
{:
	public Node root;
:}

terminal        String INT; 
terminal        BOOLEAN, CHAR, CONST, DOUBLE, FLOAT, LONG, SHORT;
terminal        CLASS, SUPER, INTERFACE, EXTENDS, IMPLEMENTS;
terminal        String VOID;
terminal        NULL;
terminal        IF, ELSE, DO, WHILE, FOR, CASE, SWITCH, BREAK;
terminal        RETURN;
terminal        String PUBLIC, PRIVATE, PROTECTED, ABSTRACT, STRICTFP, FINAL, STATIC;
terminal        ASSERT, BYTE;
terminal        CONTINUE, DEFAULT, ENUM, FINALLY, GOTO;
terminal        IMPORT, INSTANCEOF, NATIVE, NEW, PACKAGE;
terminal        SYNCHRONIZED, THIS, TRANSIENT, VOLATILE;
terminal        CATCH, TRY, THROW, THROWS;
terminal        String IDENTIFIER;
terminal        String OP_EQ, OP_GT, OP_LT, OP_NOT_EQ, OP_UNARY, OP_TERNARY, OP_LOOP_IN, OP_LAMBDA, 
                OP_EQ_EQ, OP_GT_EQ, OP_LT_EQ, OP_LOGIC_AND, OP_LOGIC_OR, OP_POS_INCR, 
                OP_NEG_INCR, OP_PLUS, OP_MINUS, OP_MULTIPLY, OP_DIVIDE, OP_AND, OP_OR, OP_XOR, 
                OP_MODULUS, OP_BIT_L_SHIFT, OP_BIT_R_SHIFT, OP_UNSIGN_R_SHIFT, OP_ADD_AND, OP_MINUS_AND, 
                OP_MULTIPLY_AND, OP_DIVIDE_AND, OP_BIT_AND, OP_BIT_INCL_OR, OP_BIT_EXCL_OR, 
                OP_MODULUS_AND, OP_L_SHIFT_AND, OP_R_SHIFT_AND, OP_UNSIGN_R_SHIFT_AND;
terminal        Integer INTEGER_LITERAL;
terminal        STRING_LITERAL;
terminal        LPAREN, RPAREN, LBRACE, RBRACE, LBRACK, RBRACK, SEMICOLON, COMMA, DOT;
terminal        UNKNOWN;

nonterminal      Node CompilationUnit, TypeDeclaration_opt, TypeDeclaration, ClassDeclaration;
nonterminal      Node NormalClassDeclaration;
nonterminal      ClassBody ClassBody, ClassBodyDeclaration, ClassBodyDeclaration_opt;
nonterminal      List<String> ClassModifier_opt;
nonterminal      Type Result;
nonterminal      String ClassModifier;
nonterminal      TempWrapper VariableDeclaratorList, VariableDeclarator; 
nonterminal      VariableDeclarator_opt;
nonterminal      VariableInitializer VariableInitializer_opt, VariableInitializer;
nonterminal      Dims_opt, Dims;
nonterminal      MethodDeclaration MethodDeclaration, ClassMemberDeclaration;
nonterminal      List<String> MethodModifier_opt;
nonterminal      String MethodModifier;
nonterminal      TempWrapper MethodHeader;
nonterminal      MethodBody MethodBody;
nonterminal      TempWrapper MethodDeclarator; 
nonterminal      List<Parameter> FormalParameterList_opt, FormalParameterList;
nonterminal      Parameter LastFormalParameter;
nonterminal      Parameter FormalParameter;
nonterminal      List<Statement> Block, BlockStatements, BlockStatement_opt; 
nonterminal      Statement BlockStatement, Statement, StatementWithoutTrailingSubstatement, ExpressionStatement, StatementExpression;
nonterminal      LocalVariableDeclarationStatement LocalVariableDeclarationStatement, LocalVariableDeclaration;
nonterminal      Type UnannType; 
nonterminal      Type UnannPrimitiveType; 
nonterminal      Type UnannReferenceType; 
nonterminal      ElementType UnannClassOrInterfaceType, UnannClassType, UnannArrayType;
nonterminal      Type NumericType, IntegralType, FloatingPointType;
nonterminal      IdentifierNode Identifier, VariableDeclaratorId;
nonterminal      IdentifierExpression ExpressionName, LeftHandSide;
nonterminal      Integer Literal;
nonterminal      IntegerLiteralExpression PrimaryNoNewArray;
nonterminal      Expression Primary, PostfixExpression, UnaryExpressionNotPlusMinus;
nonterminal      Expression UnaryExpression, MultiplicativeExpression, AdditiveExpression, ShiftExpression;
nonterminal      Expression RelationalExpression, EqualityExpression, AndExpression, ExclusiveOrExpression;
nonterminal      Expression InclusiveOrExpression, ConditionalAndExpression, ConditionalOrExpression;
nonterminal      Expression ConditionalExpression, AssignmentExpression, Expression;
nonterminal      PackageDelcaration_opt, PackageDelcaration, ImportDeclaration_opt, ImportDeclaration;
nonterminal      TypeParameters_opt, TypeParameters, Superclass_opt, Superclass, Superinterfaces_opt, Superinterfaces;
nonterminal      Throws_opt, Throws, VariableModifier_opt, VariableModifier, TypeArguments_opt, TypeArguments;
nonterminal      PreIncrementExpression, PreDecrementExpression;
nonterminal      PostIncrementExpression, PostDecrementExpression, ArrayCreationExpression;
nonterminal		 CastExpression, AmbiguousName;
nonterminal      AssignmentExpression Assignment;
nonterminal      AssignmentOperator AssignmentOperator;

/* Compilation related grammar */

CompilationUnit             ::= PackageDelcaration_opt ImportDeclaration_opt TypeDeclaration_opt;

PackageDelcaration_opt      ::= PackageDelcaration
                            | /* empty */
                            ;

ImportDeclaration_opt       ::= ImportDeclaration_opt ImportDeclaration
                            | /* empty */
                            ;

TypeDeclaration_opt         ::= TypeDeclaration_opt TypeDeclaration 
                            | /* empty */
                            ;

TypeDeclaration             ::= ClassDeclaration;

ClassDeclaration            ::= NormalClassDeclaration;

NormalClassDeclaration      ::= ClassModifier_opt:cmdOpt CLASS Identifier:i TypeParameters_opt Superclass_opt Superinterfaces_opt ClassBody:cb
							{:
								RESULT = new ClassDeclaration(i, cmdOpt, cb);
								parser.root = (Node)RESULT;
							:};

TypeParameters_opt           ::= TypeParameters
                            | /* empty */
                            ;
                               
Superclass_opt              ::= Superclass
                            | /* empty */
                            ;
                               
Superinterfaces_opt          ::= Superinterfaces
                            | /* empty */
                            ;

ClassModifier_opt           ::= ClassModifier_opt:cmdOpt ClassModifier:cm 
                            {: 
                                if (cmdOpt == null) {
                                    cmdOpt = new ArrayList<>();
                                }
                                RESULT = cmdOpt;
                                RESULT.add (cm); 
                            :}
                            | /* empty */
                            ;

ClassModifier               ::= PUBLIC:p {: RESULT = p; :} | PROTECTED:p {: RESULT = p; :} | PRIVATE:p {: RESULT = p; :} | ABSTRACT:p {: RESULT = p; :} | STATIC:s {: RESULT = (String)s; :} | FINAL:p {: RESULT = p; :} | STRICTFP:p {: RESULT = p; :};

ClassBody                   ::= LBRACE ClassBodyDeclaration_opt:cbOpt {: RESULT = cbOpt; :} RBRACE;

Identifier                  ::= IDENTIFIER:i {: RESULT = new IdentifierNode(i); :};

ClassBodyDeclaration_opt    ::= ClassBodyDeclaration_opt ClassBodyDeclaration:cb 
                            {: 
                                RESULT = cb;
                            :}
                            | /* empty */
                            ;

ClassBodyDeclaration        ::= ClassMemberDeclaration:cmd {: RESULT = new ClassBody(cmd); :};

ClassMemberDeclaration      ::= MethodDeclaration:md {: RESULT = md; :};

/* Method related grammar */

MethodDeclaration           ::= MethodModifier_opt:mmOpt MethodHeader:mh MethodBody:mb
                            {: 
                                RESULT = new MethodDeclaration(mmOpt, mh.result, mh.identifiernode, mh.parameters, mb);
                                
                            :};

MethodModifier_opt          ::= MethodModifier_opt:mmOpt MethodModifier:mm
                            {:
                                if (mmOpt == null) {
                                    mmOpt = new ArrayList<>();
                                }
                                RESULT = mmOpt;
                                RESULT.add(mm);
                            :}
                            | /* empty */
                            ;

MethodModifier              ::= PUBLIC:p {: RESULT = p; :} | PROTECTED:p {: RESULT = p; :} | PRIVATE:p {: RESULT = p; :} | ABSTRACT:p {: RESULT = p; :} | STATIC:p {: RESULT = p; :} | FINAL:p {: RESULT = p; :} | STRICTFP:p {: RESULT = p; :};

MethodHeader                ::= Result:i MethodDeclarator:md Throws_opt
                            {:
                                RESULT = new TempWrapper();
                                RESULT.result = new Result(i);
                                RESULT.identifiernode = md.identifiernode; /* TODO expand in the future */
                                RESULT.parameters = md.parameters;
                            :};

Throws_opt                  ::= Throws
                            | /* empty */
                            ;

Result                      ::= VOID:v {: RESULT = new NamedType(v); :};

MethodDeclarator            ::= Identifier:id LPAREN FormalParameterList_opt:fpl_opt RPAREN
                            {:
                                RESULT = new TempWrapper();                                
                                RESULT.identifiernode = id;
                                RESULT.parameters = fpl_opt;
                                
                            :}
                            ;

FormalParameterList_opt     ::= FormalParameterList:fpl
                            {:
                                RESULT = fpl;
                            :}
                            | /* empty */
                            ;

FormalParameterList         ::= LastFormalParameter:lfp
                            {:
                                RESULT = new ArrayList<>();
                                RESULT.add (lfp);
                            :}
                            ;

LastFormalParameter         ::= FormalParameter:fp
                            {:
                                 RESULT = fp;
                            :}
                            ;

FormalParameter             ::= VariableModifier_opt UnannType:ut VariableDeclaratorId:vdi
                            {:
                                 RESULT = new Parameter (ut, vdi);
                            :}
                            ;

VariableModifier_opt        ::= VariableModifier_opt VariableModifier
                            | /* empty */
                            ;


UnannReferenceType          ::= UnannArrayType:uat
                            {:
                                 RESULT = new ArrayType(uat);
                            :}
                            ;

UnannArrayType              ::= UnannClassOrInterfaceType:ucoit Dims
                            {:
                                 RESULT = ucoit;
                            :}
                            ;

Dims                        ::= LBRACK RBRACK;

UnannClassOrInterfaceType   ::= UnannClassType:uct
                            {:
                                 RESULT = uct;
                            :}
                            ;

UnannClassType              ::= Identifier:id TypeArguments_opt
                            {:
                                 RESULT = new ElementType(id);
                            :}
                            ;

TypeArguments_opt           ::= TypeArguments
                            | /* empty */
                            ;

/* Assignment Expression related grammar */

Expression                  ::= AssignmentExpression:ae
                            {:
                                RESULT = ae;
                            :}
                            ;

AssignmentExpression        ::= ConditionalExpression:ce
                            {:
                                RESULT = ce;
                            :}
                            | Assignment:ass
                            {:
                                 RESULT = ass;
                            :}
                            ;
                            
Assignment                  ::= LeftHandSide:lhs AssignmentOperator:ao Expression:e
                            {:
                                 RESULT = new AssignmentExpression(lhs, ao, e);
                            :}
                            ;
                            
LeftHandSide                ::= ExpressionName:en
                            {:
                                 RESULT = en;
                            :}
                            ;
                            
AssignmentOperator          ::= OP_EQ:eq
                            {:
                                 RESULT = new AssignmentOperator(eq);
                            :}
                            | OP_MULTIPLY_AND
                            | OP_DIVIDE_AND
                            | OP_MODULUS_AND
                            | OP_ADD_AND
                            | OP_MINUS_AND
                            | OP_L_SHIFT_AND
                            | OP_R_SHIFT_AND
                            | OP_UNSIGN_R_SHIFT_AND
                            | OP_BIT_AND
                            | OP_BIT_EXCL_OR
                            | OP_BIT_INCL_OR
                            ;

ConditionalExpression       ::= ConditionalOrExpression:coe
                            {:
                                RESULT = coe;
                            :}
                            | ConditionalOrExpression OP_TERNARY Expression OP_LOOP_IN ConditionalExpression
                           // {:
                                //RESULT = a;
//zyx : how to add 
//zyx : do we have this class hierarchy?
//zyx : what is this to express.
//zyx : int y = x == 42 ? 1 : 0;
                          //  :}
//zyx : here need to extend the class 
//zyx : need to condider how to build "int y = x == 42 ? 1 : 0;" this data structure.
;

ConditionalOrExpression     ::= ConditionalAndExpression:cae
                            {:
                                RESULT = cae;
                            :}
                            ;

ConditionalAndExpression    ::= InclusiveOrExpression:ioe
                            {:
                                RESULT = ioe;
                            :}
                            ;

InclusiveOrExpression       ::= ExclusiveOrExpression:eoe
                            {:
                                RESULT = eoe;
                            :}
                            ;

ExclusiveOrExpression       ::= AndExpression:ae
                            {:
                                RESULT = ae;
                            :}
							| ExclusiveOrExpression OP_XOR AndExpression
							;

AndExpression               ::= EqualityExpression:ee
                            {:
                                RESULT = ee;
                            :}
                            | AndExpression OP_LOGIC_AND EqualityExpression;

EqualityExpression          ::= RelationalExpression:re
                            {:
                                RESULT = re;
                            :}
                            | EqualityExpression OP_EQ_EQ RelationalExpression
                            | EqualityExpression OP_NOT_EQ RelationalExpression;

RelationalExpression        ::= ShiftExpression:se
                            {:
                                RESULT = se;
                            :};

ShiftExpression             ::= AdditiveExpression:ae
                            {:
                                RESULT = ae;
                            :}
                            | ShiftExpression OP_BIT_L_SHIFT AdditiveExpression
                            | ShiftExpression OP_BIT_R_SHIFT AdditiveExpression
                            | ShiftExpression OP_UNSIGN_R_SHIFT AdditiveExpression;

AdditiveExpression          ::= MultiplicativeExpression:me
                            {:
                                RESULT = me;
                            :}
							| AdditiveExpression:lhs OP_PLUS:op MultiplicativeExpression:rhs
							{:
							    RESULT = new BinaryExpression(lhs, rhs, new BinaryOperator (op));
							:}
							| AdditiveExpression OP_MINUS MultiplicativeExpression
							;

MultiplicativeExpression    ::= UnaryExpression:ue
                            {:
                                RESULT = ue;
                            :}
                            |MultiplicativeExpression OP_MULTIPLY UnaryExpression
                            |MultiplicativeExpression OP_DIVIDE UnaryExpression
                            |MultiplicativeExpression OP_MODULUS UnaryExpression;

UnaryExpression             ::= PreIncrementExpression
                            | PreDecrementExpression
                            | OP_PLUS UnaryExpression
                            | OP_MINUS UnaryExpression
                            | UnaryExpressionNotPlusMinus:unp
                            {:
                                RESULT = unp;
                            :}
                            ;
                            
UnaryExpressionNotPlusMinus ::= PostfixExpression:pe
                            {:
                                RESULT = pe;
                            :}
							| OP_UNARY UnaryExpression
							| OP_NOT_EQ UnaryExpression
							| CastExpression;

PostfixExpression           ::= Primary:p
                            {:
                                RESULT = p;
                            :}
                            | ExpressionName:en
                            {:
                                 RESULT = en;
                            :}
                            | PostIncrementExpression
                            | PostDecrementExpression
                            ;

Primary                     ::= PrimaryNoNewArray:pna
                            {:
                                RESULT = pna;
                            :}
                            | ArrayCreationExpression
                            ;
                            
ExpressionName              ::= Identifier:id
                            {:
                                 RESULT = new IdentifierExpression(id);
                            :}
                            | AmbiguousName DOT Identifier
                            ;
                            
AmbiguousName               ::= Identifier
                            | AmbiguousName DOT Identifier
                            ;

PrimaryNoNewArray           ::= Literal:l
                            {:
                                RESULT = new IntegerLiteralExpression(l);
                            :}
                            ;

Literal                     ::= INTEGER_LITERAL:il
                            {:
                                RESULT = new Integer(il);
                            :}
                            ;

/* Method body related grammar */

MethodBody                  ::= Block:b
                            {:
                                 RESULT = new MethodBody(b);
                            :}
                            ;

Block                       ::= LBRACE BlockStatements:bss RBRACE
                            {:
                                 RESULT = bss;
                            :}
                            ;

BlockStatements             ::= BlockStatement:bs BlockStatement_opt: bs_opt  //zyx : here _oprt in java SPEC is BlockStatements ::=BlockStatement {BlockStatement} //here means one or more
//zyx : this grammar decide  how many state inside the method.
//zyx : eg : int x= 4 ; int y =4;
                            {:
                                RESULT = new ArrayList<>();//zyx: 
                                RESULT.add(bs);
                                if (bs_opt != null) {//zyx : cause this is one or many. so  check if the bs_opt is null or not . (null )just ignore  (!null) need add all the statement .
// use the {blockstatemet} is quite usual. cause  normally with in the method body there are always many statemetn. 
                                	RESULT.addAll(bs_opt);//here is the ArrayList<> function addall :
//form Java spec: .addAll: Appends all of the elements in the specified collection to the end of this list, in the order that they are returned by the specified collection's iterator (optional operation).
                                }
                            :}
                            ;

BlockStatement_opt          ::= BlockStatement_opt:bs_opt BlockStatement:bs//zyx : this si the way we used to express the {BlockStatement}t this is not in the spec.
//zyx : here is the way we used to express one or many blockstatement. 
//zyx the action here we use  list to add the all hte possible statamet.
                            {:
                            	if (bs_opt == null) {//zyx: to check if this is the first time to work through BlockStatement_opt. if this is the first time just creat a new ArrayList<>().if not just get the pass the BlockStatement_opt to the BlockStatement_opt then add the BlockStatement to the ArrayList<>()
                            		bs_opt = new ArrayList<>();
                            	}
                            	RESULT = bs_opt;
                                RESULT.add(bs);
                            :}//
                            | /* empty */
                            ;

BlockStatement              ::= LocalVariableDeclarationStatement:lvds
                            {:
                                 RESULT = lvds;
                            :}
                            | Statement:s
                            {:
                                 RESULT = s;
                            :}
                            ;
                            
Statement                   ::= StatementWithoutTrailingSubstatement:swts
                            {:
                                 RESULT = swts;
                            :}
                            ;
                            
StatementWithoutTrailingSubstatement ::= Block:b
                                     {:
                                          RESULT = new StatementWithoutTrailingSubstatement(b);
                                     :}
                                     | ExpressionStatement:es
                                     {:
                                          RESULT = es;
                                     :}
                                     ;
                                     
ExpressionStatement         ::= StatementExpression:se SEMICOLON
                            {:
                                 RESULT = se;
                            :}
                            ;
                            
StatementExpression         ::= Assignment:ass
                            {:
                                 RESULT = new ExpressionStatement(ass);
                            :}
                            ;

LocalVariableDeclarationStatement ::= LocalVariableDeclaration:lvd SEMICOLON
                                  {:
                                       RESULT = lvd;
                                  :}
                                  ;

LocalVariableDeclaration    ::= VariableModifier_opt UnannType:ut VariableDeclaratorList:vdl//zyx : we did not use VariableModifier_opt here . not sure why ? what kind of side effeft will have here? 
// the UnannType will get the statemet type eg: int.
                            {:
                                 RESULT = new LocalVariableDeclarationStatement(ut, vdl.variabledeclaratorid, vdl.variableinitializer);
                            :}
                            ;

VariableDeclaratorList      ::= VariableDeclarator:vd  VariableDeclarator_opt
//zyx : here the {,VariableDeclarator} decde the if we can have "int x,y =3" this kind of old fation style.
//zyx : we do not do this . 
//zyx : so  in this cup we can resolve the "int x,y =3" .

                            {:
                                 RESULT = vd;   
                                 //RESULT = vd_opt;
                            :}
                            ;
                            
VariableDeclarator_opt      ::= COMMA VariableDeclarator_opt VariableDeclarator//:vd
                           // {:
                            //     RESULT = vd;
                            //:}
                            | /* empty */
//zyx : I think I can comment the action here. cause  we did not plan to pass ""int x,y =3" "
                            ;
                            
VariableDeclarator          ::= VariableDeclaratorId:vdi VariableInitializer_opt:vi_opt
//zyx : in java spec VariableDeclarator::= VariableDeclaratorId [=VariableInitializer].
//zyx : so here VariableInitializer_opt means one or 0.
//zyx : from the bottom defination we can see this 1/0 feature.
                            {:
                                 RESULT = new TempWrapper();
                                 RESULT.variabledeclaratorid = vdi;
                                 RESULT.variableinitializer = vi_opt;
                            :}
                            ;

VariableInitializer_opt     ::= OP_EQ VariableInitializer:vi//zyx : here is that 1/0 featuer.
//zyx  : we need this 
                            {:
                                RESULT = vi;
//zyx: how to pass 0 here ? 
                            :}
                            | /* empty */
                            //zyx : when it is zero it means : only "int x;"
                            ;
                            
VariableDeclaratorId        ::= Identifier:id Dims_opt
                            {:
                                 RESULT = id;
                            :}
                            ;

Dims_opt                    ::= Dims
                            | /* empty */
                            ;

VariableInitializer         ::= Expression:e
                            {:
                                RESULT = new VariableInitializer(e);
                            :}
                            ;

UnannType                   ::= UnannPrimitiveType:upt
                            {:
                                 RESULT = upt;
                            :}
                            
                            | UnannReferenceType:urt
                            {:
                                 RESULT = urt;
                            :}
                            ;

UnannPrimitiveType          ::= NumericType:nt
                            {:
                                 RESULT = nt;
                            :}
                            /*| BOOLEAN*/
                            ;

NumericType                 ::= IntegralType:it
                            {:
                                 RESULT = it;
                            :}
                            | FloatingPointType
                            ;

IntegralType                ::= INT:i
                            {:
                                 RESULT = new NamedType(i);
                            :}
                            | CHAR
                            | BYTE
                            | SHORT
                            | LONG
                            ;

FloatingPointType           ::= DOUBLE
                            | FLOAT
                            ;

